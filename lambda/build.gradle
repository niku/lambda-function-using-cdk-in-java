plugins {
	id 'java'
	// Code Formatter
	// https://github.com/diffplug/spotless
	id 'com.diffplug.spotless' version '5.1.0'
	// Static analyzer
	// https://github.com/google/error-prone
	id 'net.ltgt.errorprone' version '1.2.1'
	// https://github.com/spotbugs/spotbugs
	id 'com.github.spotbugs' version '4.4.4'
}

repositories {
	mavenCentral()
}

dependencies {
	errorprone 'com.google.errorprone:error_prone_core:2.4.0'
	testImplementation platform('org.junit:junit-bom:5.6.2')
	testImplementation 'org.junit.jupiter:junit-jupiter'
	implementation platform('software.amazon.awssdk:bom:2.13.70')
	implementation 'com.amazonaws:aws-lambda-java-core:1.2.1'
	implementation 'com.amazonaws:aws-lambda-java-events:2.2.9'
	implementation 'com.google.code.gson:gson:2.8.6'
}

test {
	useJUnitPlatform()
}

task buildZip(type: Zip) {
	from compileJava
	from processResources
	into('lib') {
		from configurations.runtimeClasspath
	}
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

java {
	sourceCompatibility = JavaVersion.VERSION_11
}

spotless {
	java {
		googleJavaFormat()
	}
	groovyGradle {
		greclipse()
	}
}

build.dependsOn buildZip
